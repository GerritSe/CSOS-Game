class Rect
{
  field int x, y, width, height, speed;
  field boolean enemy;

  constructor Rect new(int pX, int pY, int pWidth, int pHeight, int pSpeed)
  {
    let x = pX;
    let y = pY;
    let width = pWidth;
    let height = pHeight;
    let speed = pSpeed;
    let enemy = false;

    return this;
  }

  method void dispose()
  {
    do Memory.deAlloc(this);
    return;
  }

  method void isEnemy(boolean isEnemy)
  {
    let enemy = isEnemy;
    return;
  }

  method void draw()
  {
    var int tX, tY, tWidth, tHeight;

    if (x > 511) {
      return;
    }
    if (y > 255) {
      return;
    }

    let tX = x;
    let tY = y;
    let tWidth = width;
    let tHeight = height;

    if (x < 0)
    {
      let tX = 0;
      let tWidth = 0;

      if ((x + width) > 0) {
        let tWidth = width + x;
      }
    }

    if (y < 0)
    {
      let tY = 0;
      let tHeight = 0;

      if ((y + height) > 0) {
       let tHeight = height + y;
      }
    }

    if ((x + width) > 511) {
      let tWidth = 511 - x;
    }

    if ((y + height) > 255) {
      let tHeight = 255 - y;
    }

    if (tWidth > 0 & tHeight > 0)
    {
      do Screen.setColor(true);
      do Screen.drawRectangle(tX, tY, tX + tWidth, tY + tHeight);
      do Screen.setColor(false);
      if (enemy & (tHeight > 3) & (tWidth > 3)) {
        do Screen.drawRectangle(tX + 1, tY + 1, tX + tWidth - 1, tY + tHeight - 1);
      }
    }
    return;
  }

  method void moveUp()
  {
    let y = y - speed;
    return;
  }

  method void moveRight()
  {
    let x = x + speed;
    return;
  }

  method void moveDown()
  {
    let y = y + speed;
    return;
  }

  method void moveLeft()
  {
    let x = x - speed;
    return;
  }

  method int getX()
  {
    return x;
  }

  method int getY()
  {
    return y;
  }

  method int getWidth()
  {
    return width;
  }

  method int getHeight()
  {
    return height;
  }

  method int getSpeed()
  {
    return speed;
  }

  method void setX(int pX)
  {
    let x = pX;
    return;
  }

  method void setY(int pY)
  {
    let y = pY;
    return;
  }

  method void setWidth(int pWidth)
  {
    let width = pWidth;
    return;
  }

  method void setHeight(int pHeight)
  {
    let height = pHeight;
    return;
  }

  method void setSpeed(int pSpeed)
  {
    let speed = pSpeed;
    return;
  }
}
