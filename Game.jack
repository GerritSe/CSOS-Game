class Game
{
  field int MAX_ENEMIES, MAX_ENEMY_SIZE, MIN_ENEMY_SIZE, MIN_ENEMY_SPEED, MAX_ENEMY_SPEED, PLAYER_SPEED;
  field Rect player;
  field Array enemies;
  field boolean running;
  // Direction in which the player moves
  // 1 = up, 2 = right, etc.
  // Standing still is not possible in this game
  field int playerDirection, ticks, score, lastEnemy, lastEnemyY;

  constructor Game new()
  {
    var int i;
    var Rect rect;

    let MAX_ENEMIES     = 20;
    let MAX_ENEMY_SIZE  = 40;
    let MIN_ENEMY_SIZE  = 15;
    let MAX_ENEMY_SPEED = 4;
    let MIN_ENEMY_SPEED = 2; // Do not set below 2
    let PLAYER_SPEED    = 4;

    let player = Rect.new(0, 0, 20, 20, PLAYER_SPEED);
    let enemies = Array.new(MAX_ENEMIES);
    let running = true;
    let playerDirection = 2;
    let ticks = 0;
    let score = 0;
    let lastEnemy = 0;
    let lastEnemyY = 0;

    let i = 0;
    while (i < MAX_ENEMIES)
    {
      let rect =  Rect.new(-100 , -100, 25, 25, 0);
      do rect.isEnemy(true);
      let enemies[i] = rect;
      let i = i + 1;
    }
    return this;
  }

  method void dispose()
  {
    do enemies.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method void clock()
  {
    let ticks = ticks + 1;
    if (MathV2.mod(ticks, 50) = 0) {
      let score = score + Math.divide(MAX_ENEMY_SIZE, 20);
    }
    if (ticks > 2000) {
      let ticks = 0;
    }
    return;
  }

  method void handleInputs()
  {
    var char key;

    let key = Keyboard.keyPressed();

    if (key = 81) {
      let running = false;
    }
    if (key = 131) {
      let playerDirection = 1;
    }
    if (key = 132) {
      let playerDirection = 2;
    }
    if (key = 133) {
      let playerDirection = 3;
    }
    if (key = 130) {
      let playerDirection = 4;
    }
    if (key = 46) {
      let MAX_ENEMY_SIZE = MAX_ENEMY_SIZE + 5;
    }
    if (key = 44) {
      let MAX_ENEMY_SIZE = MAX_ENEMY_SIZE - 5;
    }
    return;
  }

  method void gameLogic()
  {
    var Rect rect;

    // Enemy size has to be in a certain range
    if (MAX_ENEMY_SIZE > 50) {
      let MAX_ENEMY_SIZE = 50;
    }
    if (MAX_ENEMY_SIZE < 15) {
      let MAX_ENEMY_SIZE = 15;
    }
    if (player.getX() < 0) {
      let playerDirection = 2;
    }
    if (player.getY() < 0) {
      let playerDirection = 3;
    }
    if ((player.getX() + player.getWidth()) > 511) {
      let playerDirection = 4;
    }
    if ((player.getY() + player.getHeight()) > 255) {
      let playerDirection = 1;
    }
    if (playerDirection = 1) {
      do player.moveUp();
    }
    if (playerDirection = 2) {
      do player.moveRight();
    }
    if (playerDirection = 3) {
      do player.moveDown();
    }
    if (playerDirection = 4) {
      do player.moveLeft();
    }

    if (MathV2.mod(ticks, Math.divide(2000, Math.multiply(MAX_ENEMIES, 7))) = 0)
    {
      let rect = enemies[lastEnemy];
      let lastEnemyY = MathV2.mod(Math.abs(MathV2.mod(ticks + player.getX(), 512 + player.getY()) + player.getX() - player.getY() - lastEnemyY), 255);
      do rect.setX(512);
      do rect.setY(lastEnemyY);

      do rect.setHeight(MathV2.mod(ticks, MAX_ENEMY_SIZE));
      if (rect.getHeight() < MIN_ENEMY_SIZE) {
        do rect.setHeight(MIN_ENEMY_SIZE);
      }
      do rect.setWidth(rect.getHeight());

      do rect.setSpeed(MathV2.mod(ticks, (MAX_ENEMY_SPEED + 1)));
      if (rect.getSpeed() = 0) {
        do rect.setSpeed(MIN_ENEMY_SPEED);
      }
      let lastEnemy = lastEnemy + 1;
      if (lastEnemy > (MAX_ENEMIES - 1)) {
        let lastEnemy = 0;
      }
    }

    do moveEnemies();
    return;
  }

  method void detectCollisions(int enemyId)
  {
    var Rect rect;

    let rect = enemies[enemyId];

    if ((player.getX() < rect.getX()) & ((player.getX() + player.getWidth()) > rect.getX()))
    {
      if ((player.getY() < rect.getY()) & ((player.getY() + player.getHeight() > rect.getY()))) {
        // BOOM
        let running = false;
      }

      if ((player.getY() > rect.getY()) & ((rect.getY() + rect.getHeight()) > player.getY())) {
        // BOOM
        let running = false;
      }

    }

    if ((player.getX() > rect.getX()) & ((rect.getX() + rect.getWidth()) > player.getX()))
    {
      if ((player.getY() < rect.getY()) & ((player.getY() + player.getHeight()) > rect.getY())) {
        // BOOM
        let running = false;
      }

      if ((player.getY() > rect.getY()) & ((rect.getY() + rect.getHeight()) > player.getY())) {
        // BOOM
        let running = false;
      }
    }
    return;
  }

  method void moveEnemies()
  {
    var int i;
    var Rect rect;

    let i = 0;

    while (i < MAX_ENEMIES)
    {
      let rect = enemies[i];
      do rect.moveLeft();
      do detectCollisions(i);
      let i = i + 1;
    }

    return;
  }

  method void render()
  {
    var int i;
    var Rect rect;

    do player.draw();

    let i = 0;
    while (i < MAX_ENEMIES)
    {
      let rect = enemies[i];
      do rect.draw();
      let i = i + 1;
    }

    // Enabling this code to output the current score, the game
    // will generate a heap overflow after 20 seconds
    // do Output.moveCursor(1, 50);
    // do Output.printString("Score: ");
    // do Output.printInt(score);
    return;
  }

  method void run()
  {
    while (running)
    {
      do handleInputs();
      do clock();
      do gameLogic();
      do Screen.clearScreen();
      do render();
      do Sys.wait(20);
    }
    return;
  }
}
